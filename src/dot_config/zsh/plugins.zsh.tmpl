# Plugins
export ZSH_PLUGINS_PATH="$XDG_DATA_HOME/zsh/plugins"

# ============================================================================
# Stage 1: Plugins that load BEFORE compinit
# ============================================================================
_load_plugins_pre_compinit() {
    # fzf
    if [ -x "$(command -v fzf)" ]; then # Check if global fzf is installed
        # fzf binary configuration
        export FZF_DEFAULT_COMMAND="fd . $HOME"
        export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
        export FZF_ALT_C_COMMAND="fd -t d . $HOME"
        export FZF_COMPLETION_TRIGGER='**'

        # fzf plugin
        if [ -f "$ZSH_PLUGINS_PATH/fzf/fzf.plugin.zsh" ]; then
            source "$ZSH_PLUGINS_PATH/fzf/fzf.plugin.zsh"
        fi

        # fzf-tab
        if [ -f "$ZSH_PLUGINS_PATH/fzf-tab/fzf-tab.plugin.zsh" ]; then
            source "$ZSH_PLUGINS_PATH/fzf-tab/fzf-tab.plugin.zsh"

            # Configuration
            # Use popup mode in tmux
            if [[ -n "$TMUX" ]]; then
                zstyle ':fzf-tab:*' fzf-command ftb-tmux-popup
                zstyle ':fzf-tab:*' popup-min-size 90 24  # Width 90 cols, height 24 lines minimum
                zstyle ':fzf-tab:*' fzf-flags --height=80% --preview-window=right:50%:wrap --border=rounded
            fi

            # Disable sort when completing `git checkout`
            zstyle ':completion:*:git-checkout:*' sort false

            # Use eza for file/directory previews when available
            if [ -x "$(command -v eza)" ]; then
                # Preview directory's content with eza when completing cd
                zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always --icons $realpath'

                # Preview file/directory with eza for general completions
                zstyle ':fzf-tab:complete:*:*' fzf-preview \
                    'if [ -d $realpath ]; then
                        eza -1 --color=always --icons $realpath
                    else
                        bat --color=always --style=numbers --line-range=:500 $realpath 2>/dev/null ||
                        cat $realpath 2>/dev/null ||
                        eza -1 --color=always --icons $realpath
                    fi'

                # Preview for ls command
                zstyle ':fzf-tab:complete:ls:*' fzf-preview 'eza -1 --color=always --icons $realpath'

                # For kill/ps commands, show process details
                zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-preview '[[ $group == "[process ID]" ]] && ps --pid=$word -o cmd --no-headers -w -w'
                zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-flags --preview-window=down:3:wrap
            fi
        fi
    fi
}

# ============================================================================
# Stage 2: Plugins that must load AFTER compinit
# ============================================================================
_load_plugins_post_compinit() {
    {{ if eq .chezmoi.os "darwin" -}}
    # atuin
    # Load after compinit to override fzf history search
    if [ -x "$(command -v atuin)" ]; then
        eval "$(atuin init zsh)"
    fi
    {{ end -}}

    # oh-my-posh
    if [ -x "$(command -v oh-my-posh)" ]; then
        eval "$(oh-my-posh init zsh --config $XDG_DATA_HOME/oh-my-posh/themes/nightowl_minimal.json)"
    fi

    # git plugin (must be after compinit for completion strategy)
    if [ -f "$ZSH_PLUGINS_PATH/git/git.plugin.zsh" ]; then
        source "$ZSH_PLUGINS_PATH/git/git.plugin.zsh"
    fi

    # zsh-autosuggestions (must be after compinit for completion strategy)
    if [ -f "$ZSH_PLUGINS_PATH/zsh-autosuggestions/zsh-autosuggestions.zsh" ]; then
        # Use both history and completion for suggestions
        ZSH_AUTOSUGGEST_STRATEGY=(history completion)
        # Accept suggestion with right arrow key (default behavior)
        ZSH_AUTOSUGGEST_ACCEPT_WIDGETS=(forward-char end-of-line vi-forward-char vi-end-of-line)
        # Fetch suggestions asynchronously for better performance
        ZSH_AUTOSUGGEST_USE_ASYNC=true

        source "$ZSH_PLUGINS_PATH/zsh-autosuggestions/zsh-autosuggestions.zsh"
    fi

    # zoxide
    if [ -x "$(command -v zoxide)" ]; then
        eval "$(zoxide init zsh)"
    fi
}
# ============================================================================
# Stage 3: Plugins that must load LAST
# ============================================================================
_load_plugins_final() {
    # zsh-syntax-highlighting LAST (after compinit and everything else)
    if [ -f "$ZSH_PLUGINS_PATH/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]; then
        source "$ZSH_PLUGINS_PATH/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
    fi
}
